//file:noinspection VulnerableLibrariesLocal
dependencies {
    implementation project(':common')
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    //kafka
    implementation 'org.springframework.kafka:spring-kafka'
    //test
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    //elasticsearch
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // Testcontainers core
    testImplementation "org.testcontainers:testcontainers:1.19.8"

    // Elasticsearch container
    testImplementation "org.testcontainers:elasticsearch:1.19.8"

    // JUnit 5 연동
    testImplementation "org.testcontainers:junit-jupiter:1.19.8"

    //DB
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework.boot:spring-boot-starter-aop'
}

bootJar {
    enabled = true
}

jib {
    from {
        image = 'eclipse-temurin:17-jre'
    }
    container {
        ports = ['8082']
    }
}

//jacoco
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }
            excludes = [
                    'store._0982.elasticsearch.ElasticSearchApplication*',
                    'store._0982.elasticsearch.presentation.dto.*',
                    'store._0982.elasticsearch.application.dto.*',
                    'store._0982.elasticsearch.config.*',
                    'store._0982.elasticsearch.exception.*',
                    'store._0982.elasticsearch.domain.*',
                    'store._0982.elasticsearch.*Document*'
            ]
        }
    }
}

test {
    // 카프카 병렬 테스트 오류 방지
    maxParallelForks = 1
    // jacoco
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
